apply plugin: 'com.android.application'
apply plugin: 'com.android.databinding'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'ext-properties'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.example.realm_template"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        // debug {
        //     storeFile rootProject.file("debug.keystore")
        // }

        release
    }

    buildTypes {
        //noinspection GroovyMissingReturnStatement
        release {
            minifyEnabled true
            shrinkResources true // shrinkResources true requires minifyEnabled true

            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile file('proguard-rules.pro')

            // Configure release signing if ext.properties provides certificate information
            // To enable release signing, copy ext.properties.forReleaseSigning to ext.properties and edit.
            if (project.hasProperty('storeFile')) {
                android.signingConfigs.release.storeFile = rootProject.file(storeFile)
                android.signingConfigs.release.storePassword = storePassword
                android.signingConfigs.release.keyAlias = keyAlias
                android.signingConfigs.release.keyPassword = keyPassword

                signingConfig signingConfigs.release
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

repositories {
    // for stetho-realm
    maven {
        url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo'
    }
}

project.ext {
    supportPackageVersion = '23.0.1'
    supportTestVersion = '0.4'
    espressoVersion = '2.2.1'
    junitVersion = '4.12'
    dexmakerVersion = '1.2'
    powerMockVersion = '1.6.2'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    apt 'com.android.databinding:compiler:1.0-rc2'

    compile "com.android.support:appcompat-v7:${supportPackageVersion}"
    compile "com.android.support:support-annotations:${supportPackageVersion}"
    provided 'javax.annotation:javax.annotation-api:1.2'

    compile 'io.realm:realm-android:0.82.2'

    compile 'com.google.dagger:dagger:2.0.1'
    apt "com.google.dagger:dagger-compiler:2.0.1"

    debugCompile 'com.facebook.stetho:stetho:1.2.0'
    debugCompile 'com.uphyca:stetho_realm:0.7.2'

    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:1.3.1"

    androidTestApt "com.google.dagger:dagger-compiler:2.0.1"
    androidTestCompile "junit:junit:${junitVersion}"
    androidTestCompile "com.android.support.test.espresso:espresso-core:${espressoVersion}"
    androidTestCompile "com.android.support.test:runner:${supportTestVersion}"
    androidTestCompile "com.android.support.test:rules:${supportTestVersion}"
    androidTestCompile "com.android.support:support-annotations:${supportPackageVersion}"

    // mockito for dex
    androidTestCompile "com.google.dexmaker:dexmaker:${dexmakerVersion}"
    androidTestCompile "com.google.dexmaker:dexmaker-mockito:${dexmakerVersion}"

    testCompile "junit:junit:${junitVersion}"
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile "com.android.support.test:runner:${supportTestVersion}"
    testCompile "com.android.support.test:rules:${supportTestVersion}"
    testCompile "com.android.support:support-annotations:${supportPackageVersion}"

    testCompile "org.powermock:powermock-module-junit4:${powerMockVersion}"
    testCompile "org.powermock:powermock-module-junit4-rule:${powerMockVersion}"
    testCompile("org.powermock:powermock-api-mockito:${powerMockVersion}") {
        // to exclude wrong version of hamcrest-core. use mockito-core instead.
        exclude module: 'mockito-all'
    }
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile "org.powermock:powermock-classloading-xstream:${powerMockVersion}"
}
